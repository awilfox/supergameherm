include(CheckIncludeFiles)
include(CheckFunctionExists)
include(CheckSymbolExists)
include(TestBigEndian)

macro(posix_check)
	check_include_files(unistd.h HAVE_UNISTD_H)
	if(HAVE_UNISTD_H)
		check_symbol_exists(_POSIX_VERSION unistd.h HAVE_POSIX)
	endif()
endmacro()

macro(windows_check)
	check_include_files(windows.h HAVE_WINDOWS)
endmacro()

macro(stdc_check)
	check_include_files(stdbool.h HAVE_STDBOOL_H)
	check_include_files(stdint.h HAVE_STDINT_H)
endmacro()

macro(swap_check)
	check_include_files(endian.h HAVE_ENDIAN_H)
	check_include_files(sys/endian.h HAVE_SYS_ENDIAN_H)

	if(NOT(HAVE_ENDIAN_H OR HAVE_SYS_ENDIAN_H))
		# Check for __bswap_XX
		check_function_exists(__bswap_16 HAVE_BSWAP_16)
		check_function_exists(__bswap_32 HAVE_BSWAP_32)
		if(NOT(HAVE_BSWAP_16 OR HAVE_BSWAP_32))
			check_function_exists(_byteswap_ushort
				HAVE_BYTESWAP_USHORT)
			check_function_exists(_byteswap_ulong
				HAVE_BYTESWAP_ULONG)
		endif()
	else()
		add_definitions(-D_BSD_SOURCE)
	endif()
endmacro()

macro(clock_check)
	if(HAVE_POSIX)
		check_symbol_exists(clock_gettime time.h HAVE_CLOCK_GETTIME)

		if(NOT(HAVE_CLOCK_GETTIME))
			# OS X check
			check_include_files("mach/mach.h;mach/clock.h" HAVE_MACH_CLOCK_H)
		endif()

		check_symbol_exists(nanosleep time.h HAVE_NANOSLEEP)
		check_symbol_exists(clock_nanosleep time.h HAVE_CLOCK_NANOSLEEP)
	endif()
endmacro()

macro(mmap_check)
	check_symbol_exists(mmap sys/mman.h HAVE_MMAP)
	if(HAVE_MMAP)
		check_symbol_exists(mremap sys/mman.h HAVE_MREMAP)
		check_symbol_exists(MAP_ANON sys/mman.h HAVE_MAP_ANONYMOUS)
		if(NOT HAVE_MAP_ANONYMOUS)
			if(${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
				add_definitions(-D_DARWIN_C_SOURCE)
				check_symbol_exists(MAP_ANON sys/mman.h HAVE_MAP_ANONYMOUS)
			elseif(${CMAKE_SYSTEM_NAME} MATCHES "FreeBSD")
				add_definitions(-D__BSD_VISIBLE)
				check_symbol_exists(MAP_ANON sys/mman.h HAVE_MAP_ANONYMOUS)
			elseif(${CMAKE_SYSTEM_NAME} MATCHES "Linux")
				check_symbol_exists(MAP_ANONYMOUS sys/mman.h HAVE_MAP_ANONYMOUS)
			endif()
		endif()
	endif()
endmacro()

macro(platform_checks)
	posix_check()
	if(NOT HAVE_POSIX)
		windows_check()
	elseif(${CMAKE_SYSTEM_NAME} MATCHES "Linux")
		set(GLIBC_FEATURE "-D_XOPEN_SOURCE=700")
		set(CMAKE_REQUIRED_DEFINITIONS ${GLIBC_FEATURE})
		add_definitions(${GLIBC_FEATURE})
	endif()

	test_big_endian(BIG_ENDIAN)
	if(NOT BIG_ENDIAN)
		set(LITTLE_ENDIAN 1)
	endif()

	stdc_check()
	swap_check()
	clock_check()
	if(HAVE_POSIX)
		mmap_check()
	endif()
endmacro()
